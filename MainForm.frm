VERSION 5.00
Object = "{831FDD16-0C5C-11D2-A9FC-0000F8754DA1}#2.2#0"; "MSCOMCTL.OCX"
Begin VB.Form FrmMain 
   Caption         =   "Vb6Tkinter https://github.com/cdhigh"
   ClientHeight    =   8130
   ClientLeft      =   45
   ClientTop       =   675
   ClientWidth     =   12975
   BeginProperty Font 
      Name            =   "Courier New"
      Size            =   9
      Charset         =   0
      Weight          =   400
      Underline       =   0   'False
      Italic          =   0   'False
      Strikethrough   =   0   'False
   EndProperty
   Icon            =   "MainForm.frx":0000
   LinkTopic       =   "Form1"
   ScaleHeight     =   542
   ScaleMode       =   3  'Pixel
   ScaleWidth      =   865
   StartUpPosition =   2  'ÆÁÄ»ÖÐÐÄ
   Begin VB.ComboBox cmbEditCombo 
      CausesValidation=   0   'False
      Height          =   345
      ItemData        =   "MainForm.frx":058A
      Left            =   7560
      List            =   "MainForm.frx":058C
      TabIndex        =   11
      Text            =   "Combo1"
      Top             =   720
      Visible         =   0   'False
      Width           =   1095
   End
   Begin VB.ComboBox cmbEditList 
      Height          =   345
      ItemData        =   "MainForm.frx":058E
      Left            =   6360
      List            =   "MainForm.frx":0590
      Style           =   2  'Dropdown List
      TabIndex        =   12
      Top             =   720
      Visible         =   0   'False
      Width           =   1095
   End
   Begin Vb6Tkinter.xpcmdbutton CmdRefsFormsList 
      Height          =   495
      Left            =   120
      TabIndex        =   0
      Top             =   120
      Width           =   2295
      _ExtentX        =   4048
      _ExtentY        =   873
      Caption         =   "Refresh Forms(&R)"
      BeginProperty Font {0BE35203-8F91-11CE-9DE3-00AA004BB851} 
         Name            =   "Courier New"
         Size            =   9
         Charset         =   0
         Weight          =   400
         Underline       =   0   'False
         Italic          =   0   'False
         Strikethrough   =   0   'False
      EndProperty
   End
   Begin MSComctlLib.StatusBar stabar 
      Align           =   2  'Align Bottom
      Height          =   377
      Left            =   0
      TabIndex        =   10
      Top             =   7748
      Width           =   12974
      _ExtentX        =   22886
      _ExtentY        =   661
      Style           =   1
      _Version        =   393216
      BeginProperty Panels {8E3867A5-8586-11D1-B16A-00C0F0283628} 
         NumPanels       =   1
         BeginProperty Panel1 {8E3867AB-8586-11D1-B16A-00C0F0283628} 
            Bevel           =   0
            Object.Width           =   2687
            MinWidth        =   2687
         EndProperty
      EndProperty
      BeginProperty Font {0BE35203-8F91-11CE-9DE3-00AA004BB851} 
         Name            =   "Courier New"
         Size            =   9
         Charset         =   0
         Weight          =   400
         Underline       =   0   'False
         Italic          =   0   'False
         Strikethrough   =   0   'False
      EndProperty
   End
   Begin VB.ComboBox cmbFrms 
      Height          =   345
      ItemData        =   "MainForm.frx":0592
      Left            =   120
      List            =   "MainForm.frx":0594
      Style           =   2  'Dropdown List
      TabIndex        =   5
      Top             =   840
      Width           =   2415
   End
   Begin VB.TextBox TxtTips 
      BeginProperty Font 
         Name            =   "Courier New"
         Size            =   10.5
         Charset         =   0
         Weight          =   400
         Underline       =   0   'False
         Italic          =   0   'False
         Strikethrough   =   0   'False
      EndProperty
      Height          =   2775
      Left            =   120
      Locked          =   -1  'True
      MultiLine       =   -1  'True
      ScrollBars      =   2  'Vertical
      TabIndex        =   7
      Top             =   4920
      Width           =   2415
   End
   Begin VB.ListBox LstComps 
      Height          =   3210
      Left            =   120
      TabIndex        =   6
      Top             =   1200
      Width           =   2415
   End
   Begin Vb6Tkinter.GridOcx LstCfg 
      Height          =   6855
      Left            =   2640
      TabIndex        =   8
      Top             =   840
      Width           =   6015
      _ExtentX        =   10610
      _ExtentY        =   12091
      BeginProperty Font {0BE35203-8F91-11CE-9DE3-00AA004BB851} 
         Name            =   "Courier New"
         Size            =   9
         Charset         =   0
         Weight          =   400
         Underline       =   0   'False
         Italic          =   0   'False
         Strikethrough   =   0   'False
      EndProperty
   End
   Begin VB.TextBox TxtCode 
      BeginProperty Font 
         Name            =   "Courier New"
         Size            =   10.5
         Charset         =   0
         Weight          =   400
         Underline       =   0   'False
         Italic          =   0   'False
         Strikethrough   =   0   'False
      EndProperty
      Height          =   6855
      Left            =   8760
      MultiLine       =   -1  'True
      ScrollBars      =   3  'Both
      TabIndex        =   9
      Top             =   840
      Width           =   4095
   End
   Begin Vb6Tkinter.xpcmdbutton CmdGenCode 
      Height          =   495
      Left            =   2760
      TabIndex        =   1
      Top             =   120
      Width           =   2295
      _ExtentX        =   4048
      _ExtentY        =   873
      Caption         =   "Generate Code(&G)"
      BeginProperty Font {0BE35203-8F91-11CE-9DE3-00AA004BB851} 
         Name            =   "Courier New"
         Size            =   9
         Charset         =   0
         Weight          =   400
         Underline       =   0   'False
         Italic          =   0   'False
         Strikethrough   =   0   'False
      EndProperty
   End
   Begin Vb6Tkinter.xpcmdbutton CmdCopyToClipboard 
      Height          =   495
      Left            =   5340
      TabIndex        =   2
      Top             =   120
      Width           =   2295
      _ExtentX        =   4048
      _ExtentY        =   873
      Caption         =   "Copy to Clipboard(&C)"
      BeginProperty Font {0BE35203-8F91-11CE-9DE3-00AA004BB851} 
         Name            =   "Courier New"
         Size            =   9
         Charset         =   0
         Weight          =   400
         Underline       =   0   'False
         Italic          =   0   'False
         Strikethrough   =   0   'False
      EndProperty
   End
   Begin Vb6Tkinter.xpcmdbutton CmdSaveToFile 
      Height          =   495
      Left            =   7950
      TabIndex        =   3
      Top             =   120
      Width           =   2295
      _ExtentX        =   4048
      _ExtentY        =   873
      Caption         =   "Save to File(&F)"
      BeginProperty Font {0BE35203-8F91-11CE-9DE3-00AA004BB851} 
         Name            =   "Courier New"
         Size            =   9
         Charset         =   0
         Weight          =   400
         Underline       =   0   'False
         Italic          =   0   'False
         Strikethrough   =   0   'False
      EndProperty
   End
   Begin Vb6Tkinter.xpcmdbutton CmdQuit 
      Height          =   495
      Left            =   10560
      TabIndex        =   4
      Top             =   120
      Width           =   2295
      _ExtentX        =   4048
      _ExtentY        =   873
      Caption         =   "Quit(&Q)"
      BeginProperty Font {0BE35203-8F91-11CE-9DE3-00AA004BB851} 
         Name            =   "Courier New"
         Size            =   9
         Charset         =   0
         Weight          =   400
         Underline       =   0   'False
         Italic          =   0   'False
         Strikethrough   =   0   'False
      EndProperty
   End
   Begin VB.Menu mnuFile 
      Caption         =   "File(&F)"
      Begin VB.Menu mnuRefreshForms 
         Caption         =   "Refresh Forms(&R)"
         Shortcut        =   ^R
      End
      Begin VB.Menu mnuSeparator1 
         Caption         =   "-"
      End
      Begin VB.Menu mnuGenCode 
         Caption         =   "Generate Code(&G)"
         Shortcut        =   ^G
      End
      Begin VB.Menu mnuSeparator10 
         Caption         =   "-"
      End
      Begin VB.Menu mnuSaveToFile 
         Caption         =   "Save Code to File(&F)"
         Begin VB.Menu mnuSaveAll 
            Caption         =   "Save All Code(&A)"
         End
         Begin VB.Menu mnuSaveUiOnly 
            Caption         =   "Save Class UI Only(&G)"
         End
      End
      Begin VB.Menu mnuCopyToClipboard 
         Caption         =   "Copy Code To Clipboard(&C)"
         Begin VB.Menu mnuCopyToClipAll 
            Caption         =   "Copy All Code(&A)"
         End
         Begin VB.Menu mnuCopyToClipUiOnly 
            Caption         =   "Copy Class UI Only(&G)"
         End
      End
      Begin VB.Menu mnuSeparator2 
         Caption         =   "-"
      End
      Begin VB.Menu mnuAddProperty 
         Caption         =   "Add One Property(&P)"
      End
      Begin VB.Menu mnuSeparator3 
         Caption         =   "-"
      End
      Begin VB.Menu mnuQuit 
         Caption         =   "Quit(&Q)"
      End
   End
   Begin VB.Menu mnuOptions 
      Caption         =   "Options(&O)"
      Begin VB.Menu mnuV2andV3Code 
         Caption         =   "Compatible Code for Python 2.x/3.x(&C)"
      End
      Begin VB.Menu mnuUseTtk 
         Caption         =   "Use TTK Themed Library(&T)"
         Checked         =   -1  'True
      End
      Begin VB.Menu mnuRelPos 
         Caption         =   "Use Relative Position(&R)"
         Checked         =   -1  'True
      End
      Begin VB.Menu mnuI18n 
         Caption         =   "Support i18n(&I)"
      End
      Begin VB.Menu mnuUnicodePrefixU 
         Caption         =   "Add A Prefix 'u' to Unicode String(&U)"
      End
      Begin VB.Menu mnuSeparator5 
         Caption         =   "-"
      End
      Begin VB.Menu mnuPythonExe 
         Caption         =   "Set diretory of python.exe(&E)..."
      End
   End
   Begin VB.Menu mnuTools 
      Caption         =   "Tools(&T)"
      Begin VB.Menu mnuPreview 
         Caption         =   "Preview(&P)"
         Enabled         =   0   'False
         Shortcut        =   {F5}
      End
      Begin VB.Menu mnuEncodeAFile 
         Caption         =   "Encode File to Base64(&B)"
      End
      Begin VB.Menu mnuSeparator6 
         Caption         =   "-"
      End
      Begin VB.Menu mnuCheckUpdate 
         Caption         =   "Check Update(&U)"
      End
      Begin VB.Menu mnuAbout 
         Caption         =   "About(&A)"
      End
   End
   Begin VB.Menu mnuLanguage 
      Caption         =   "Language(&L)"
      Begin VB.Menu mnuLng 
         Caption         =   "English(&E)"
         Index           =   0
      End
   End
End
Attribute VB_Name = "FrmMain"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = True
Attribute VB_Exposed = False
Option Explicit

Public mConnect As Connect

'2012.11.23£¬ÎªÁË¹ö¶¯ÌõºÍÁÐ±í¿òµÄ°ó¶¨·½±ã£¬½«ÆäÐÞ¸ÄÎªÈ«¾Ö±äÁ¿g_Comps£¬·ÅÔÚCommon.basÖÐ
'Private m_Comps() As Object             'ºÍLstCompsÐÐÊýÒ»Ñù¶à£¬¶ÔÓ¦¸÷×é¼þÉú³ÉµÄÊµÀý
Private m_MainMenu As clsMenu              '²Ëµ¥¶ÔÏó
Private m_PrevCompIdx As Long
Private m_curFrm As Object
Private m_prevsf As String
Private m_nLngNum As Long                   ' ÓïÑÔÖÖÀà
Private m_HasCommonDialog As Boolean
Private m_saTmpFile() As String
Private m_TxtCodeExpanded As Boolean
Private m_TxtTipsExpanded As Boolean
Private m_BriefCaption As String

Private Declare Function GetSystemDefaultLCID Lib "kernel32" () As Long

Private Sub Form_Load()
    
    Dim s As String
    ReDim g_Comps(0) As Object
    
    g_AppVerString = App.Major & "." & App.Minor & IIf(App.Revision > 0, "." & App.Revision, "")
    g_DefaultFontName = ""
    m_HasCommonDialog = False
    m_TxtCodeExpanded = False
    m_TxtTipsExpanded = False
    
    ReDim m_saTmpFile(0) As String
    
    '¶àÓïÖÖÖ§³Ö
    InitMultiLanguage
    
    LstCfg.Redraw = False
    LstCfg.Editable = True
    LstCfg.EditType = EnterKey Or MouseDblClick Or F2Key
    LstCfg.CheckBoxes = True
    LstCfg.AddColumn "Property", 2260, lgAlignCenterCenter
    LstCfg.AddColumn "Value", 3450, lgAlignCenterCenter
    LstCfg.ColAlignment(0) = lgAlignLeftCenter
    LstCfg.ColAlignment(1) = lgAlignLeftCenter
    LstCfg.SelectBackColor = &HFCC597 'vbHighlight
    LstCfg.Redraw = True
    
    m_BriefCaption = "Vb6Tkinter v" & g_AppVerString
    #If DebugVer Then
        m_BriefCaption = m_BriefCaption & " [Debug Mode] "
    #End If
    Me.Caption = m_BriefCaption
    
    mnuV2andV3Code.Checked = GetSetting(App.Title, "Settings", "V2andV3Code", "0") = "1"
    mnuUseTtk.Checked = GetSetting(App.Title, "Settings", "UseTtk", "1") = "1"
    mnuRelPos.Checked = GetSetting(App.Title, "Settings", "RelPos", "1") = "1"
    mnuI18n.Checked = GetSetting(App.Title, "Settings", "i18n", "1") = "1"
    mnuUnicodePrefixU.Checked = GetSetting(App.Title, "Settings", "UnicodePrefix", "0") = "1"
    g_bUnicodePrefixU = mnuUnicodePrefixU.Checked
    
    g_PythonExe = GetSetting(App.Title, "Settings", "PythonExe", "")
    
    Set cmbEditList.Font = LstCfg.Font
    Set cmbEditCombo.Font = LstCfg.Font
    
    ResizeInit Me
    
    CmdRefsFormsList_Click
    
End Sub

'¶àÓïÖÖÖ§³Ö³õÊ¼»¯
Private Sub InitMultiLanguage()
    
    Dim I As Long, s As String, sa() As String
    
    If Not LngFileExist() Then
        m_nLngNum = 0
        mnuLng(0).Checked = True
        Exit Sub
    End If
    
    sa = GetAllLanguageName()
    mnuLng(0).Caption = sa(0)
    m_nLngNum = 1
    For I = 1 To UBound(sa)
        Load mnuLng(I)
        mnuLng(I).Caption = sa(I)
        m_nLngNum = m_nLngNum + 1
    Next
    
    'ÇÐ»»ÓïÑÔ£¬×¢²á±í±£´æµÄÓïÑÔÓÅÏÈ£¬Æä´Î¸ù¾Ý²Ù×÷ÏµÍ³Ñ¡Ôñ
    s = GetSetting(App.Title, "Settings", "Language", "")
    I = m_nLngNum
    If Len(s) Then                                                              'Ñ¡ÔñÖ®Ç°±£´æµÄÓïÑÔÖÖÀà£¬Èç¹û´æÔÚµÄ»°
        For I = 0 To m_nLngNum - 1
            If mnuLng(I).Caption = s Then
                ChangeLanguage (mnuLng(I).Caption)
                mnuLng(I).Checked = True
                Exit For
            End If
        Next
    End If
    
    '³¢ÊÔÅÐ¶Ï²Ù×÷ÏµÍ³ÓïÖÖ
    If I > m_nLngNum - 1 Then
        
        I = GetSystemDefaultLCID()
        If I = &H804 Or I = &H4 Or I = &H1004 Then
            s = "¼òÌåÖÐÎÄ"
        ElseIf I = &H404 Or I = &HC04 Then
            s = "·±ówÖÐÎÄ"
        ElseIf I Mod 16 = 9 Then
            s = "English"
        Else                                                                    'ÆäËûÓïÑÔÏÈ°´Ó¢Óï´¦Àí£¬´ýÈí¼þÆô¶¯ºóÓÃ»§ÔÙÑ¡ÔñºÏÊÊµÄÓïÑÔ
            s = "English"
        End If
        
        For I = 0 To m_nLngNum - 1
            If InStr(1, mnuLng(I).Caption, s) > 0 Then
                ChangeLanguage (mnuLng(I).Caption)
                mnuLng(I).Checked = True
                Exit For
            End If
        Next
        
        ' ÎÞ·¨×Ô¶¯È·ÈÏÓïÖÖ£¬Ä¬ÈÏÑ¡ÔñµÚÒ»¸ö
        If I > m_nLngNum - 1 Then
            ChangeLanguage (mnuLng(0).Caption)
            mnuLng(0).Checked = True
        End If
    End If
    
End Sub

Private Sub CmdQuit_Click()
    mConnect.Hide
End Sub

Private Sub cmbFrms_Click()
    
    Dim frm As Object
    
    '²éÕÒµ½¶ÔÓ¦µÄ´°ÌåÒýÓÃ
    Set m_curFrm = Nothing
    If Len(cmbFrms.Text) Then
        For Each frm In VbeInst.ActiveVBProject.VBComponents
            If frm.Type = vbext_ct_VBForm And frm.Name = cmbFrms.Text Then
                Set m_curFrm = frm
                Exit For
            End If
        Next
    End If
    
    m_PrevCompIdx = -1
    
    '½«¿Ø¼þÌí¼Óµ½ÁÐ±í
    If Not ResetLstComps(m_curFrm) Then
        LstComps.Clear
        LstCfg.Clear
        TxtTips.Text = L("l_TipHasNoControl", "Has no control on Form, please add at least one control on it.")
        m_PrevCompIdx = -1
    Else
        LstComps.ListIndex = 0
        LstComps_Click
    End If
    
    If LstComps.ListCount > 0 Then
        CmdGenCode.Enabled = True
        CmdCopyToClipboard.Enabled = True
        CmdSaveToFile.Enabled = True
        mnuSaveToFile.Enabled = True
        mnuCopyToClipboard.Enabled = True
        mnuAddProperty.Enabled = True
        mnuGenCode.Enabled = True
    Else
        CmdGenCode.Enabled = False
        CmdCopyToClipboard.Enabled = False
        CmdSaveToFile.Enabled = False
        mnuSaveToFile.Enabled = False
        mnuCopyToClipboard.Enabled = False
        mnuAddProperty.Enabled = False
        mnuGenCode.Enabled = False
    End If
    
End Sub

Private Sub CmdCopyToClipboard_Click()
    Me.PopupMenu mnuCopyToClipboard
End Sub

'¸üÐÂ¸÷¸öÁÐ±í£¬´´½¨¶ÔÓ¦µÄ¿Ø¼þÀàÊµÀý, ·µ»ØFalse±íÊ¾³õÊ¼»¯Ê§°Ü£¬True±íÊ¾³É¹¦
Private Function ResetLstComps(frm As Object) As Long
    
    Dim Obj As Object, ObjClsModule As Object, I As Long, s As String, j As Long, idx As Long
    Dim nScaleMode As Long, nScaleWidth As Long, nScaleHeight As Long
    Dim CodeMember As Member, CodeMembers As Members, dMethods As New Dictionary
    Dim ctlsIgnored As String
    
    ResetLstComps = False
    If frm Is Nothing Then Exit Function
    
    LstComps.Clear
    'Erase g_Comps
    Set m_MainMenu = Nothing
    
    '´´½¨´°ÌåÊµÀý×öÎªÁÐ±íµÚÒ»Ïî
    ReDim g_Comps(0) As Object
    Set g_Comps(0) = New clsForm
    
    'ÒòÎªScaleX/ScaleYÎª´°ÌåÀà¶ÀÓÐ·½·¨£¬Ö»ÄÜÏÈÔÚÕâÀï×ª»»´°Ìå´óÐ¡ÎªÏñËØµ¥Î»
    nScaleWidth = Round(ScaleX(frm.Properties("ScaleWidth"), frm.Properties("ScaleMode"), vbPixels))
    nScaleHeight = Round(ScaleY(frm.Properties("ScaleHeight"), frm.Properties("ScaleMode"), vbPixels))
    g_Comps(0).InitConfig frm, nScaleWidth, nScaleHeight, dMethods
    g_Comps(0).Name = WTOP
    LstComps.AddItem g_Comps(0).Name & " (Form)"
    I = 1
    
    m_HasCommonDialog = False
    
    '»ñÈ¡´°ÌåµÄ´úÂëÄ£¿éÖÐËùÓÐµÄ¹ý³Ìº¯ÊýÁÐ±í£¬±£´æÎªÒ»¸ö×Öµä¶ÔÏó£¬´«Èë¸÷ÀàÄ£¿é£¬ÓÃÓÚ×Ô¶¯Éú³É¶ÔÓ¦µÄbindcommand
    If Not frm.CodeModule Is Nothing Then
        Set CodeMembers = frm.CodeModule.Members
        If Not CodeMembers Is Nothing Then
            For Each CodeMember In CodeMembers
                If CodeMember.Type = vbext_mt_Method Then
                    idx = InStrRev(CodeMember.Name, "_")
                    If idx > 1 Then
                        s = Left$(CodeMember.Name, idx - 1)
                        If dMethods.Exists(s) Then
                            dMethods.Item(s) = dMethods.Item(s) & "," & CodeMember.Name & "," 'Ê¹ÓÃ¶ººÅ×öÎª·Ö¸ô·û·½±ã²éÕÒ
                        Else
                            dMethods.Item(s) = "," & CodeMember.Name & ","
                        End If
                    End If
                End If
            Next
        End If
    End If
    
    
    '½«¿Ø¼þÌí¼Óµ½ÁÐ±íÖÐ
    For Each Obj In frm.Designer.VBControls
        
        CreateObj Obj, ObjClsModule                                             'Éú³É¶ÔÓ¦ÀàÄ£¿éÊµÀý
        
        If Not ObjClsModule Is Nothing Then
            
            'ÓÃÓÚ×Ô¶¯µ¥Î»×ª»»£¬ÐèÒªÔÚInitConfigÖ®Ç°ÉèÖÃÕâ¸öÖµ
            ObjClsModule.ScaleMode = frm.Properties("ScaleMode")
            
            'Èç¹û´°Ìå´æÔÚ²Ëµ¥¿Ø¼þ£¬Ôò´´½¨Ö÷²Ëµ¥¶ÔÏó£¬Ö÷²Ëµ¥¿Ø¼þ½«¹ÜÀíËùÓÐµÄ²Ëµ¥Ïî
            If Obj.ClassName = "Menu" And m_MainMenu Is Nothing Then
                ReDim Preserve g_Comps(I) As Object
                Set m_MainMenu = New clsMenu
                Set g_Comps(I) = m_MainMenu
                LstComps.AddItem m_MainMenu.Name & " (MainMenu)"
                m_MainMenu.InitConfig
                I = I + 1
            End If
            
            'Ìí¼Ó¿Ø¼þµ½¿Ø¼þÁÐ±í
            ReDim Preserve g_Comps(I) As Object
            Set g_Comps(I) = ObjClsModule
            LstComps.AddItem Obj.Properties("Name") & " (" & Obj.ClassName & ")"
            
            '³õÊ¼»¯¸÷¿Ø¼þ¶ÔÓ¦µÄÀàÄ£¿é¶ÔÏó
            If Obj.Container Is frm.Designer Then
                g_Comps(I).Parent = IIf(Obj.ClassName = "Menu", "MainMenu", WTOP)
                g_Comps(I).InitConfig Obj, frm.Properties("ScaleWidth"), frm.Properties("ScaleHeight"), dMethods
            ElseIf Obj.Container.ClassName = "Menu" Then  '×Ó²Ëµ¥
                g_Comps(I).Parent = Obj.Container.Properties("Name")
                g_Comps(I).InitConfig Obj, 0, 0, dMethods
            Else
                On Error Resume Next
                nScaleMode = Obj.Container.Properties("ScaleMode")
                nScaleWidth = Obj.Container.Properties("ScaleWidth")
                nScaleHeight = Obj.Container.Properties("ScaleHeight")
                If Err.Number Then         'FrameºÍ¸ö±ðÆäËûÈÝÆ÷²»Ö§³ÖScaleWidthÊôÐÔ£¬ÔòÊ¹ÓÃWidth´úÌæ
                    nScaleMode = vbTwips
                    nScaleWidth = Me.ScaleX(Obj.Container.Properties("Width"), frm.Properties("ScaleMode"), vbTwips)
                    nScaleHeight = Me.ScaleY(Obj.Container.Properties("Height"), frm.Properties("ScaleMode"), vbTwips)
                End If
                Err.Clear
                On Error GoTo 0
                g_Comps(I).ScaleMode = nScaleMode
                g_Comps(I).Parent = Obj.Container.Properties("Name")
                g_Comps(I).InitConfig Obj, nScaleWidth, nScaleHeight, dMethods
            End If
            
            I = I + 1
            ResetLstComps = True
        ElseIf Obj.ClassName = "CommonDialog" Then
            m_HasCommonDialog = True
        ElseIf Len(ctlsIgnored) = 0 Or InStr(1, ctlsIgnored, Obj.ClassName & ",") <= 0 Then
            If MsgBox(L_F("l_msgCtlNotSupport", "The addin not support '{0}' control (Name:{1}).\n\nIt will not be processed.\n\n'Ok' to continue.\n'Cancel' for ignoring controls of same type.", _
                Obj.ClassName, Obj.Properties("Name")), vbInformation + vbOKCancel, App.Title) = vbCancel Then
                ctlsIgnored = ctlsIgnored & Obj.ClassName & ","
            End If
        End If
    Next  'frm.Designer.VBControls
    
    'Éú³É²Ëµ¥µÄÊ÷ÐÎ²ã´Î¹ØÏµ£¬ÎªÉú³É´úÂë½¨Á¢»ù´¡
    CreateMenuHiberarchy
    
    'ÕûÀíNotebookºÍÆä¸÷Ò³Ç©ÄÚ¿Ø¼þµÄ¸¸×Ó¹ØÏµ
    ArrangeNotebookAndSubWidgets
    
    '³¢ÊÔ×Ô¶¯½«¹ö¶¯Ìõ°ó¶¨µ½¶ÔÓ¦µÄ¿Ø¼þ
    TryAssignScrollbar2Widgets
    
    'ÕýÈ·ÉèÖÃComboboxAdapterµÄTTKÊôÐÔ
    For I = 1 To UBound(g_Comps)
        If TypeName(g_Comps(I)) = "clsComboboxAdapter" Then g_Comps(I).TTK = mnuUseTtk.Checked
    Next
    
End Function

'Éú³ÉÒ»¸ö¿Ø¼þ×Ö·ûÊµÀý¶ÔÏó:ÊäÈëctlobj:¿Ø¼þ¶ÔÏó£¬clsobj:¶ÔÓ¦µÄ×Ö·û´®¶ÔÏó
Private Function CreateObj(ByRef ctlobj As Object, ByRef clsobj As Object) As Object
    Dim o As Object, sName As String, idx As Long
    
    Select Case ctlobj.ClassName:
        Case "Label"
            Set clsobj = New clsLabel
        Case "CommandButton"
            Set clsobj = New clsButton
        Case "TextBox"
            If ctlobj.Properties("MultiLine") Then Set clsobj = New clsText Else Set clsobj = New clsEntry
        Case "CheckBox"
            Set clsobj = New clsCheckbutton
        Case "OptionButton"
            Set clsobj = New clsRadiobutton
        Case "ComboBox"
            Set clsobj = New clsComboboxAdapter
            clsobj.TTK = mnuUseTtk.Checked
        Case "ListBox"
            Set clsobj = New clsListbox
        Case "HScrollBar", "VScrollBar"
            Set clsobj = New clsScrollbar
        Case "Slider"
            Set clsobj = New clsScale
        Case "Frame"
            'ÅÐ¶ÏÊÇ·ñÊÇTabStrip¿Ø¼þµÄÒ»Ò³
            idx = InStr(2, ctlobj.Properties("Name"), "__Tab") '´Ó2¿ªÊ¼ÖÁÉÙ±£Ö¤__TabÇ°ÓÐÒ»¸ö×Ö·û
            If idx > 1 And Not m_curFrm Is Nothing Then
                'Ñ­»·²éÑ¯ÊÇ·ñÓÐºÏÊÊµÄTabStrip¿Ø¼þ
                Set clsobj = Nothing
                sName = Left$(ctlobj.Properties("Name"), idx - 1)
                For Each o In m_curFrm.Designer.VBControls
                    If o.ClassName = "TabStrip" And o.Properties("Name") = sName Then
                        Set clsobj = New clsNotebookTab  'Ê¹ÓÃTabÀàÀ´´úÌæFrame
                        Exit For
                    End If
                Next
                If clsobj Is Nothing Then Set clsobj = New clsLabelFrame 'Ã»ÓÐ¶ÔÓ¦µÄTabStrip¶ÔÏó
            Else
                Set clsobj = New clsLabelFrame
            End If
        Case "PictureBox"
            'ÅÐ¶ÏÊÇ·ñÊÇTabStrip¿Ø¼þµÄÒ»Ò³
            idx = InStr(2, ctlobj.Properties("Name"), "__Tab") '´Ó2¿ªÊ¼ÖÁÉÙ±£Ö¤__TabÇ°ÓÐÒ»¸ö×Ö·û
            If idx > 1 And Not m_curFrm Is Nothing Then
                'Ñ­»·²éÑ¯ÊÇ·ñÓÐºÏÊÊµÄTabStrip¿Ø¼þ
                Set clsobj = Nothing
                sName = Left$(ctlobj.Properties("Name"), idx - 1)
                For Each o In m_curFrm.Designer.VBControls
                    If o.ClassName = "TabStrip" And o.Properties("Name") = sName Then
                        Set clsobj = New clsNotebookTab  'Ê¹ÓÃTabÀàÀ´´úÌæPictureBox
                        Exit For
                    End If
                Next
                If clsobj Is Nothing Then Set clsobj = New clsCanvas 'Ã»ÓÐ¶ÔÓ¦µÄTabStrip¶ÔÏó
            Else
                Set clsobj = New clsCanvas
            End If
        Case "Menu"
            Set clsobj = New clsMenuItem
        Case "ProgressBar"
            Set clsobj = New clsProgressBar                                         'ÐèÒªÆôÓÃTTK²ÅÖ§³Ö
            mnuUseTtk.Checked = True
        Case "TreeView"
            Set clsobj = New clsTreeview                                            'ÐèÒªÆôÓÃTTK²ÅÖ§³Ö
            mnuUseTtk.Checked = True
        Case "TabStrip"
            Set clsobj = New clsNotebook                                            'ÐèÒªÆôÓÃTTK²ÅÖ§³Ö
            mnuUseTtk.Checked = True
        Case "Line"
            Set clsobj = New clsSeparator
            mnuUseTtk.Checked = True
        Case "StatusBar"
            Set clsobj = New clsStatusbar
        Case Else:
            Set clsobj = Nothing
    End Select
    
    Set CreateObj = clsobj
    
End Function

'Éú³É²Ëµ¥µÄÊ÷ÐÎ²ã´Î¹ØÏµ£¬ÎªÉú³É´úÂë½¨Á¢»ù´¡
'×ÓÀà´¢´æ¸¸ÀàµÄÃû×Ö£¬¸¸Àà´¢´æËùÓÐ×ÓÀàµÄÒýÓÃ
Private Sub CreateMenuHiberarchy()

    Dim I As Long, j As Long
    If Not m_MainMenu Is Nothing Then
        For I = 0 To UBound(g_Comps)
            If TypeName(g_Comps(I)) = "clsMenu" Then
                '½«ËùÓÐµÄ¶¥²ã²Ëµ¥×öÎªclsMenuµÄ×Ó¿Ø¼þ
                For j = 0 To UBound(g_Comps)
                    If TypeName(g_Comps(j)) = "clsMenuItem" And g_Comps(j).Parent = "MainMenu" Then
                        g_Comps(I).AddChild g_Comps(j)
                    End If
                Next
            ElseIf TypeName(g_Comps(I)) = "clsMenuItem" Then
                '×Ó²Ëµ¥ÓÐ¿ÉÄÜ»¹ÓÐ×Ó²Ëµ¥
                For j = 0 To UBound(g_Comps)
                    If TypeName(g_Comps(j)) = "clsMenuItem" And g_Comps(j).Parent = g_Comps(I).Name Then
                        g_Comps(I).AddChild g_Comps(j)
                    End If
                Next
            End If
        Next
    End If
    
End Sub

'ÕûÀíÑ¡Ïî¿¨¿Ø¼þºÍÆäÄÚ²¿¿Ø¼þµÄ¸¸×Ó¹ØÏµ
Private Sub ArrangeNotebookAndSubWidgets()

    Dim I As Long, j As Long, k As Long, L As Long, idx As Long, ctlNum As Long
    Dim sTabName As String, sNbName As String, sTmp As String
    
    If UBound(g_Comps) <= 0 Then  ' 0¹Ì¶¨Îª¶¥²ã´°Ìå
        Exit Sub
    End If
    
    ctlNum = UBound(g_Comps)
    For I = 1 To ctlNum
        If TypeName(g_Comps(I)) = "clsNotebookTab" Then
            sTabName = g_Comps(I).Name
            idx = InStr(2, sTabName, "__Tab")
            If idx > 1 Then
                sNbName = Left$(sTabName, idx - 1) ' Notebook¿Ø¼þÃû
                For j = 1 To ctlNum
                    If TypeName(g_Comps(j)) = "clsNotebook" And g_Comps(j).Name = sNbName Then
                        '»ñÈ¡TABºÅ
                        sTmp = Right$(sTabName, 1)
                        If sTmp >= "1" And sTmp <= "9" Then '×î¶àÖ§³Ö9¸ö±êÇ©Ò³
                            g_Comps(j).AddTab g_Comps(I), CLng(sTmp)  ' ¼ÓÈëNotebook¶ÔÏó
                            g_Comps(I).EnableOutByMainForm = False
                            
                            '´Ë±êÇ©Ò³ÄÚËùÓÐ¿Ø¼þ¾ùÓÐclsNotebookTabÀ´½Ó¹Ü£¬²»ÔÙÓÉÖ÷´°¿ÚÊä³ö´úÂë
                            For k = 1 To ctlNum
                                If g_Comps(k).Parent = sTabName Then
                                    g_Comps(k).EnableOutByMainForm = False
                                    g_Comps(I).AddSubWidget g_Comps(k)
                                    
                                    ' ÍòÒ»±êÇ©Ò³ÄÚ»¹ÓÐÆäËûÈÝÆ÷¿Ø¼þ
                                    If TypeName(g_Comps(k)) = "clsCanvas" Or TypeName(g_Comps(k)) = "clsLabelFrame" Then
                                        For L = 1 To ctlNum
                                            If g_Comps(L).Parent = g_Comps(k).Name Then
                                                g_Comps(L).EnableOutByMainForm = False
                                                g_Comps(I).AddSubWidget g_Comps(L)
                                            End If
                                        Next
                                    End If
                                End If
                            Next
                            
                        End If
                    End If
                Next
            End If
        End If
    Next

End Sub

'½øÐÐÒ»Ð©·ÖÎö£¬³¢ÊÔ½«¹ö¶¯Ìõ×Ô¶¯°ó¶¨µ½ºÏÊÊµÄ¿Ø¼þ£¬²»Ò»¶¨³É¹¦£¬¶øÇÒ¿ÉÄÜÎóÅÐ£¬Ö»ËãÊÇ¾¡Á¦¶øÎª
Private Sub TryAssignScrollbar2Widgets()
    
    Dim I As Long, ctlNum As Long, Obj As Object, o As Object, oName As String
    Dim vX1 As Long, vY1 As Long, vX2 As Long, vY2 As Long
    Dim oX1 As Long, oY1 As Long, oX2 As Long, oY2 As Long
    Dim thresholdX1 As Long, thresholdY1 As Long
    Dim thresholdX2 As Long, thresholdY2 As Long
    Dim isWidgetScrl As Boolean, Assigned As Boolean
    
    If UBound(g_Comps) <= 0 Or m_curFrm Is Nothing Then  ' 0¹Ì¶¨Îª¶¥²ã´°Ìå
        Exit Sub
    End If
    
    'Ë®Æ½·½ÏòºÍ´¹Ö±·½Ïò¶¼Ê¹ÓÃ20¸öÏñËØ×öÎª²éÕÒ¿Ø¼þµÄÃÅÏÞ
    thresholdX1 = Round(ScaleX(20, vbPixels, m_curFrm.Properties("ScaleMode")))
    thresholdY1 = Round(ScaleY(20, vbPixels, m_curFrm.Properties("ScaleMode")))
    thresholdX2 = Round(ScaleX(5, vbPixels, m_curFrm.Properties("ScaleMode"))) '5¸öÏñËØÊÇÔÊÐí¿Ø¼þºÍ¹ö¶¯ÌõÖØµþµÄ²¿·Ö
    thresholdY2 = Round(ScaleY(5, vbPixels, m_curFrm.Properties("ScaleMode")))
    
    ctlNum = UBound(g_Comps)
    For Each Obj In m_curFrm.Designer.VBControls
        If Obj.ClassName = "HScrollBar" Then
            'Ë®Æ½¹ö¶¯Ìõ£¬ÔòÅÐ¶ÏÆäÉÏ·½ÓÐÃ»ÓÐÐèÒªÉèÖÃ¹ö¶¯ÌõµÄ¿Ø¼þ
            'vX1,vY1,vX2,vY2¹¹³ÉÒ»¸ö¾ØÐÎ£¬Èç¹ûÆäËû¿Ø¼þµÄ×óÏÂ½ÇºÍÓÒÏÂ½ÇÂäÔÚÕâ¸ö¾ØÐÎÄÚ£¬ÔòÈÏÎª¹ö¶¯Ìõ¶ÔÓ¦
            vX1 = Obj.Properties("Left") - thresholdX1
            If vX1 < 0 Then vX1 = 0
            vY1 = Obj.Properties("Top") - thresholdY1
            If vY1 < 0 Then vY1 = 0
            vX2 = Obj.Properties("Left") + Obj.Properties("Width") + thresholdX1
            vY2 = Obj.Properties("Top") + thresholdY2
            Assigned = False
            For Each o In m_curFrm.Designer.VBControls
                If (o.Container Is Obj.Container) And _
                    InStr(1, "PictureBox,ListBox,TreeView,TextBox,", o.ClassName & ",") > 0 Then  'Ö»ÓÐÕâÐ©¿Ø¼þ¿ÉÄÜÐèÒª¹ö¶¯Ìõ
                    isWidgetScrl = True
                    If o.ClassName = "TextBox" Then 'Ö»ÓÐ¶àÐÐÎÄ±¾¿ò²ÅÖ§³Ö¹ö¶¯
                        If Not o.Properties("MultiLine") Then
                            isWidgetScrl = False
                        End If
                    End If
                    If isWidgetScrl Then
                        oX1 = o.Properties("Left")
                        oY1 = o.Properties("Top") + o.Properties("Height")
                        oX2 = oX1 + o.Properties("Width")
                        oY2 = oY1
                        
                        'µÚÒ»ÐÐÎª×óÏÂ½ÇÅÐ¶Ï£¬µÚ¶þÐÐÎªÓÒÏÂ½ÇÅÐ¶Ï
                        If (oX1 >= vX1 And oX1 <= vX2 And oY1 >= vY1 And oY1 <= vY2) _
                            And (oX2 >= vX1 And oX2 <= vX2 And oY2 >= vY1 And oY2 <= vY2) Then
                            'ÉèÖÃ¿Ø¼þµÄxscrollcommandÊôÐÔ
                            oName = o.Properties("Name")
                            For I = 1 To ctlNum
                                If g_Comps(I).Name = oName Then
                                    g_Comps(I).SetSingleConfig ("xscrollcommand|" & Obj.Properties("Name") & ".set")
                                    Assigned = True
                                    Debug.Print oName & " assigned to " & Obj.Properties("name")
                                    Exit For
                                End If
                            Next
                        End If
                    End If
                End If
                If Assigned Then
                    Exit For
                End If
            Next
        ElseIf Obj.ClassName = "VScrollBar" Then
            '´¹Ö±¹ö¶¯Ìõ£¬ÔòÅÐ¶ÏÆä×ó·½ÓÐÃ»ÓÐÐèÒªÉèÖÃ¹ö¶¯ÌõµÄ¿Ø¼þ
            'vX1,vY1,vX2,vY2¹¹³ÉÒ»¸ö¾ØÐÎ£¬Èç¹ûÆäËû¿Ø¼þµÄÓÒÉÏ½ÇºÍÓÒÏÂ½ÇÂäÔÚÕâ¸ö¾ØÐÎÄÚ£¬ÔòÈÏÎª¹ö¶¯Ìõ¶ÔÓ¦
            vX1 = Obj.Properties("Left") - thresholdX1
            If vX1 < 0 Then vX1 = 0
            vY1 = Obj.Properties("Top") - thresholdY1
            If vY1 < 0 Then vY1 = 0
            vX2 = Obj.Properties("Left") + thresholdX2
            vY2 = Obj.Properties("Top") + Obj.Properties("Height") + thresholdY1
            Assigned = False
            For Each o In m_curFrm.Designer.VBControls
                If (o.Container Is Obj.Container) And _
                    InStr(1, "PictureBox,ListBox,TreeView,TextBox,", o.ClassName & ",") > 0 Then 'Ö»ÓÐÕâÐ©¿Ø¼þ¿ÉÄÜÐèÒª¹ö¶¯Ìõ
                    isWidgetScrl = True
                    If o.ClassName = "TextBox" Then 'Ö»ÓÐ¶àÐÐÎÄ±¾¿ò²ÅÖ§³Ö¹ö¶¯
                        If Not o.Properties("MultiLine") Then
                            isWidgetScrl = False
                        End If
                    End If
                    If isWidgetScrl Then
                        oX1 = o.Properties("Left") + o.Properties("Width")
                        oY1 = o.Properties("Top")
                        oX2 = oX1
                        oY2 = oY1 + o.Properties("Height")
                        
                        'µÚÒ»ÐÐÎªÓÒÉÏ½ÇÅÐ¶Ï£¬µÚ¶þÐÐÎªÓÒÏÂ½ÇÅÐ¶Ï
                        If (oX1 >= vX1 And oX1 <= vX2 And oY1 >= vY1 And oY1 <= vY2) _
                            And (oX2 >= vX1 And oX2 <= vX2 And oY2 >= vY1 And oY2 <= vY2) Then
                            'ÉèÖÃ¿Ø¼þµÄyscrollcommandÊôÐÔ
                            oName = o.Properties("Name")
                            For I = 1 To ctlNum
                                If g_Comps(I).Name = oName Then
                                    g_Comps(I).SetSingleConfig ("yscrollcommand|" & Obj.Properties("Name") & ".set")
                                    Debug.Print oName & " assigned to " & Obj.Properties("name")
                                    Assigned = True
                                    Exit For
                                End If
                            Next
                        End If
                    End If
                End If
                If Assigned Then
                    Exit For
                End If
            Next
        End If
    Next
    
End Sub

'´´½¨´úÂë
Private Sub CmdGenCode_Click()
    Dim I As Long, cnt As Long, o As Object, sysImport As String
    Dim strHead As New cStrBuilder, strOut As New cStrBuilder, strCmd As New cStrBuilder, strI18n As New cStrBuilder, strTmp As New cStrBuilder
    Dim s As String, finalCode As String, sF As String
    Dim OutOnlyV3 As Boolean, OutRelPos As Boolean, i18n As Boolean, usettk As Boolean
    Dim bUnicodePrefix As Boolean  'ÁÙÊ±±£´æUNICODEÇ°×º·½Ê½
    Dim aCompsSorted() As Object 'ÓÃÓÚÅÅÐòµÄ´úÂëÊä³ö
    
    If LstComps.ListCount = 0 Or LstCfg.ItemCount = 0 Or m_curFrm Is Nothing Then
        Exit Sub
    End If
    
    On Error Resume Next
    s = m_curFrm.Name
    If Err.Number Then
        If MsgBox(L("l_msgGetAttrOfFrmFailed", "Failed in getting property of the form, please reopen the vb project and retry.\nRefresh list of forms now?"), vbInformation + vbYesNo) = vbYes Then
            CmdRefsFormsList_Click
        End If
        Exit Sub
    End If
    Err.Clear
    On Error GoTo 0
    
    OutOnlyV3 = Not mnuV2andV3Code.Checked
    OutRelPos = mnuRelPos.Checked
    i18n = mnuI18n.Checked
    usettk = mnuUseTtk.Checked
    
    '¾ø¶Ô×ø±êBUGÌáÊ¾
'    If Not OutRelPos And m_curFrm.Properties("ScaleMode") <> vbTwips Then
'        'Èç¹ûÊ¹ÓÃ¾ø¶Ô×ø±ê£¬ÔòFrame¿Ø¼þ½öÖ§³ÖvbTwipsÄ£Ê½
'        For Each o In m_curFrm.Designer.VBControls
'            If o.ClassName = "Frame" Then
'                MsgBox L("l_msgFrameNotSupportInAbs", "The control 'Frame' is not support when menu 'Use Relative Position' unchecked."), vbInformation
'                Exit Sub
'            End If
'        Next
'    End If
'
    bUnicodePrefix = g_bUnicodePrefixU 'ÏÈÔÝ´æ£¬ÔÚº¯Êý×îºó»Ö¸´
    If OutOnlyV3 Then
        g_bUnicodePrefixU = False  'V3Ä£Ê½ÏÂ²»ÐèÒªÈÎºÎÇ°×º
    End If
    
    'ÔÚÊä³ö´úÂëÇ°ÏÈ¸üÐÂÒ»ÏÂµ±Ç°ÏÔÊ¾µÄÊý¾Ý
    UpdateCfgtoCls LstComps.ListIndex
    
    sysImport = IIf(i18n, "import os, sys, gettext", "import os, sys")
    If OutOnlyV3 Then                                                           'Êä³ö½öÕë¶ÔPYTHON 3.XµÄ´úÂë
        strHead.Append "#!/usr/bin/env python3"
        strHead.Append "#-*- coding:utf-8 -*-" & vbCrLf
        strHead.Append sysImport
        strHead.Append "from tkinter import *"
        strHead.Append "from tkinter.font import Font"
        If usettk Then strHead.Append "from tkinter.ttk import *"
        strHead.Append "#Usage:showinfo/warning/error,askquestion/okcancel/yesno/retrycancel"
        strHead.Append "from tkinter.messagebox import *"
        strHead.Append IIf(m_HasCommonDialog, "", "#") & "from tkinter import filedialog  #.askopenfilename()"
        strHead.Append IIf(m_HasCommonDialog, "", "#") & "from tkinter import simpledialog  #.askstring()"
        If m_HasCommonDialog Then
            strHead.Append "from tkinter import colorchooser  #.askcolor()"
        End If
        strHead.Append vbCrLf
    Else
        strHead.Append "#!/usr/bin/env python"
        strHead.Append "#-*- coding:utf-8 -*-" & vbCrLf
        strHead.Append sysImport
        strHead.Append "if sys.version_info[0] == 2:"
        strHead.Append "    from Tkinter import *"
        strHead.Append "    from tkFont import Font"
        If usettk Then
            strHead.Append "    from ttk import *"
        End If
        strHead.Append "    #Usage:showinfo/warning/error,askquestion/okcancel/yesno/retrycancel"
        strHead.Append "    from tkMessageBox import *"
        strHead.Append "    #Usage:f=tkFileDialog.askopenfilename(initialdir='E:/Python')"
        strHead.Append "    " & IIf(m_HasCommonDialog, "", "#") & "import tkFileDialog"
        strHead.Append "    " & IIf(m_HasCommonDialog, "", "#") & "import tkSimpleDialog"
        If m_HasCommonDialog Then
            strHead.Append "    import tkColorChooser  #askcolor()"
        End If
        strHead.Append "else:  #Python 3.x"
        strHead.Append "    from tkinter import *"
        strHead.Append "    from tkinter.font import Font"
        If usettk Then
            strHead.Append "    from tkinter.ttk import *"
        End If
        strHead.Append "    from tkinter.messagebox import *"
        strHead.Append "    " & IIf(m_HasCommonDialog, "", "#") & "import tkinter.filedialog as tkFileDialog"
        strHead.Append "    " & IIf(m_HasCommonDialog, "", "#") & "import tkinter.simpledialog as tkSimpleDialog    #askstring()"
        If m_HasCommonDialog Then
            strHead.Append "    import tkinter.colorchooser as tkColorChooser  #askcolor()"
        End If
        strHead.Append vbCrLf
    End If
    
    'Èç¹û´æÔÚ×´Ì¬À¸¿Ø¼þ£¬ÔòÏÈÊä³ö×´Ì¬À¸¿Ø¼þµÄÀà¶¨Òå
    For I = 1 To UBound(g_Comps)  '0¹Ì¶¨Îª´°Ìå£¬²»ÓÃÅÐ¶Ï
        If TypeName(g_Comps(I)) = "clsStatusbar" Then
            strHead.Append g_Comps(I).WidgetCode(OutOnlyV3)
            Exit For
        End If
    Next
    
    'Èç¹ûÓÐ¿Ø¼þÉèÖÃÁËToolTipText£¬ÔòÏÈÊä³öTooptipµÄ×Ô¶¨ÒåÀà¶¨Òå
    For I = 1 To UBound(g_Comps)  '0¹Ì¶¨Îª´°Ìå£¬²»ÓÃÅÐ¶Ï
        If g_Comps(I).hasAttribute("tooltip") And g_Comps(I).GetAttrCurrentValue("tooltip") <> "" Then
            CreateTooltipClassCode strHead
            Exit For
        End If
    Next
    
    '¹ú¼Ê»¯Ö§³Öº¯ÊýÍ·
    If i18n Then
        strI18n.Append vbCrLf & "    def retranslateUi(self):"
    End If
    
    'ÃüÁî»Øµ÷´úÂë¿é
    strTmp.Reset
    If i18n Then
        strTmp.Append vbCrLf & "        tr = gettext.translation('lang', localedir='i18n', languages=['en'], fallback=True)"
        strTmp.Append "        tr.install()"
        strTmp.Append "        self.retranslateUi()"
    End If
    strCmd.Append vbCrLf
    strCmd.Append "class Application(Application_ui):"
    strCmd.Append "    def __init__(self, master):"
    If OutOnlyV3 Then
        strCmd.Append "        super().__init__(master)" & strTmp.toString(vbCrLf) & vbCrLf
    Else
        strCmd.Append "        Application_ui.__init__(self, master)" & strTmp.toString(vbCrLf) & vbCrLf
    End If
    strTmp.Reset
    
    'Ö÷GUI´úÂë¿é
    strOut.Append "class Application_ui(Frame):"
    strOut.Append "    def __init__(self, master):"
    If OutOnlyV3 Then
        strOut.Append "        super().__init__(master)"
    Else
        strOut.Append "        Frame.__init__(self, master)"
    End If
    g_Comps(0).toString strOut, strCmd, strI18n, OutRelPos, usettk  'g_Comps(0)¹Ì¶¨ÊÇForm
    strOut.Append "        self.createWidgets()" & vbCrLf
    
    strOut.Append "    def createWidgets(self):"
    strOut.Append "        self." & WTOP & " = self.winfo_toplevel()" & vbCrLf
    If usettk Then strOut.Append "        self.style = Style()" & vbCrLf
    
    '¸ù¾ÝÒÀÀµ¹ØÏµÅÅÐò¿Ø¼þµÄÏÈºóË³Ðò
    cnt = 0
    For I = 1 To UBound(g_Comps)
        If g_Comps(I).EnableOutByMainForm Then
            ReDim Preserve aCompsSorted(cnt) As Object
            Set aCompsSorted(cnt) = g_Comps(I)
            cnt = cnt + 1
        End If
    Next
    If cnt > 0 Then
        SortWidgets aCompsSorted, cnt
    End If
    
    '±éÀú¸÷¿Ø¼þ£¬ÓÉ¸÷¿Ø¼þ×Ô¼ºÊä³ö×Ô¼ºµÄ½çÃæÉú³É´úÂë
    For I = 0 To cnt - 1
        aCompsSorted(I).toString strOut, strCmd, strI18n, OutRelPos, usettk
        strOut.Append ""  'Á½¸ö¿Ø¼þÖ®¼äÊ¹ÓÃÒ»¸ö¿ÕÐÐ¸ô¿ª
    Next
    
    'Æ´½Ó¸÷²¿·Ö´úÂë£¬Êä³öµ½ÎÄ±¾¿ò
    If Not i18n Then
        strI18n.Reset
    End If
    strCmd.Append "if __name__ == ""__main__"":"
    strCmd.Append "    " & WTOP & " = Tk()"
    strCmd.Append "    Application(" & WTOP & ").mainloop()"
    strCmd.Append vbCrLf
    finalCode = strHead.toString(vbCrLf) & strOut.toString(vbCrLf) & strI18n.toString(vbCrLf) & strCmd.toString(vbCrLf)
    
    'VBµÄTEXTBOX×î¶àÖ§³Ö65KÎÄ±¾
    If Len(finalCode) > 65000 Then
        MsgBox L("l_msgCodeTooBig", "Size of generated code is too big to load into TextBox, please choose a file to save it."), vbInformation
        sF = FileDialog(Me, True, L("l_fdSave", "Save file to:"), "*.py", m_prevsf)
        
        If Len(sF) > 0 Then
            If Len(FileExt(sF)) = 0 Then sF = sF & ".py"  'Èç¹ûÎÄ¼þÃûÃ»ÓÐÀ©Õ¹Ãû£¬×Ô¶¯Ìí¼Ó.pyÀ©Õ¹Ãû
            Utf8File_Write_VB sF, finalCode
            m_prevsf = sF
        End If
        TxtCode.Text = ""
    Else
        TxtCode.Text = finalCode
    End If
    
    strOut.Reset
    strHead.Reset
    strCmd.Reset
    strI18n.Reset
    
    g_bUnicodePrefixU = bUnicodePrefix    '»Ö¸´UNICODEÇ°×ºÄ£Ê½
End Sub

'ÊäÈë×Ô¶¨ÒåµÄTooltipÀà
'ÒòÎªVB²»ÔÊÐíÌ«¶àµÄÐøÐÐ·û£¬ËùÒÔ·Ö³ÉÈý²¿·Ö£¬ÔÚ³ÌÐòÖÐÔÙÁ¬½ÓÆðÀ´
'´Ë¿Ø¼þµÄÊ¹ÓÃ·½·¨£º
'  self.t = Tooltip(self.widget, 'tooltip text')
Private Sub CreateTooltipClassCode(ByRef strOut As cStrBuilder)
    Const strCls1 As String = "class Tooltip:" & vbCrLf & _
"    def __init__(self, widget, text, bg='#FFFFEA', pad=(5, 3, 5, 3), waittime=500, wraplength=300):" & vbCrLf & _
"        self.waittime = waittime" & vbCrLf & _
"        self.wraplength = wraplength" & vbCrLf & _
"        self.widget = widget" & vbCrLf & _
"        self.text = text" & vbCrLf & _
"        self.widget.bind('<Enter>', self.onEnter)" & vbCrLf & _
"        self.widget.bind('<Leave>', self.onLeave)" & vbCrLf & _
"        self.widget.bind('<ButtonPress>', self.onLeave)" & vbCrLf & _
"        self.bg = bg" & vbCrLf & _
"        self.pad = pad" & vbCrLf & _
"        self.id_ = None" & vbCrLf & _
"        self.tw = None" & vbCrLf & vbCrLf & _
"    def onEnter(self, event=None):" & vbCrLf & _
"        self.schedule()" & vbCrLf & vbCrLf & _
"    def onLeave(self, event=None):" & vbCrLf & _
"        self.unschedule()" & vbCrLf & _
"        self.Hide()" & vbCrLf & vbCrLf & _
"    def schedule(self):" & vbCrLf & _
"        self.unschedule()" & vbCrLf & _
"        self.id_ = self.widget.after(self.waittime, self.Show)" & vbCrLf

    Const strCls2 As String = vbCrLf & _
"    def unschedule(self):" & vbCrLf & _
"        id_ = self.id_" & vbCrLf & _
"        self.id_ = None" & vbCrLf & _
"        if id_:" & vbCrLf & _
"            self.widget.after_cancel(id_)" & vbCrLf & vbCrLf & _
"    def Show(self):" & vbCrLf & _
"        def tip_pos_calculator(widget, label, pad=(5, 3, 5, 3), tip_delta=(15, 10)):" & vbCrLf & _
"            s_width, s_height = widget.winfo_screenwidth(), widget.winfo_screenheight()" & vbCrLf & _
"            width, height = (pad[0] + label.winfo_reqwidth() + pad[2], pad[1] + label.winfo_reqheight() + pad[3])" & vbCrLf & _
"            mouse_x, mouse_y = widget.winfo_pointerxy()" & vbCrLf & _
"            x1, y1 = mouse_x + tip_delta[0], mouse_y + tip_delta[1]" & vbCrLf & _
"            if x1 + width > s_width:" & vbCrLf & _
"                x1 = mouse_x - tip_delta[0] - width" & vbCrLf & _
"            if y1 + height > s_height - 30:" & vbCrLf & _
"                y1 = mouse_y - tip_delta[1] - height" & vbCrLf & _
"                if y1 < 0:" & vbCrLf & _
"                    Y1 = 0" & vbCrLf & _
"            return x1, y1" & vbCrLf & vbCrLf & _
"        self.Hide()" & vbCrLf & _
"        self.tw = Toplevel(self.widget)" & vbCrLf & _
"        self.tw.wm_overrideredirect(True)" & vbCrLf & _
"        label = Label(self.tw, text=self.text, justify=LEFT, background=self.bg, relief=RAISED, borderwidth=1, wraplength=self.wraplength)" & vbCrLf & _
"        label.pack(ipadx=1)"

    Const strCls3 As String = vbCrLf & _
"        x, y = tip_pos_calculator(self.widget, label, self.pad)" & vbCrLf & _
"        self.tw.wm_geometry('+%d+%d' % (x, y))" & vbCrLf & vbCrLf & _
"    def Hide(self):" & vbCrLf & _
"        tw = self.tw" & vbCrLf & _
"        if tw:" & vbCrLf & _
"            tw.destroy()" & vbCrLf & _
"        self.tw = None" & vbCrLf & vbCrLf

    strOut.Append strCls1 & strCls2 & strCls3
    
End Sub

Private Sub CmdRefsFormsList_Click()
    
    Dim frm As Object, nScaleMode As Long, emptyForms() As String, emptyFormsCount As Integer, frmName As String, idx As Integer
    
    emptyFormsCount = 0
    ReDim emptyForms(emptyFormsCount) As String
    
    cmbFrms.Clear
    LstComps.Clear
    LstCfg.Clear
    
    If VbeInst.ActiveVBProject Is Nothing Then
        CmdGenCode.Enabled = False
        CmdCopyToClipboard.Enabled = False
        CmdSaveToFile.Enabled = False
        mnuSaveToFile.Enabled = False
        mnuCopyToClipboard.Enabled = False
        mnuAddProperty.Enabled = False
        mnuGenCode.Enabled = False
        Exit Sub
    End If
    
    Me.Caption = m_BriefCaption & " [" & VbeInst.ActiveVBProject.Name & "]"
    
    '²éÕÒ¹¤³ÌÖÐËùÓÐµÄ´°Ìå,È«²¿Ìí¼Óµ½×éºÏ¿ò¹©Ñ¡ÔñÊä³ö
    For Each frm In VbeInst.ActiveVBProject.VBComponents
        If frm.Type = vbext_ct_VBForm Then
            nScaleMode = frm.Properties("ScaleMode")
            If nScaleMode <> vbTwips And nScaleMode <> vbPoints And nScaleMode <> vbPixels Then
                MsgBox L_F("l_msgFailedScaleMode", "Found form'{0}', but its ScaleMode={1}, now can only support mode 1/2/3.", _
                         frm.Properties("Name"), nScaleMode), vbInformation
            ElseIf frm.Designer.VBControls.Count = 0 Then 'Ã»ÓÐ¿Ø¼þµÄ¿Õ´°Ìå·Åµ½×éºÏ¿òÁÐ±íºóÃæ
                emptyFormsCount = emptyFormsCount + 1
                ReDim Preserve emptyForms(emptyFormsCount) As String
                emptyForms(emptyFormsCount - 1) = frm.Name
            Else
                cmbFrms.AddItem frm.Name
            End If
        End If
    Next
    
    'ÔÚÁÐ±íºóÌí¼ÓÃ»ÓÐ¿Ø¼þµÄ´°Ìå
    If emptyFormsCount > 0 Then
        For idx = 0 To UBound(emptyForms)
            frmName = emptyForms(idx)
            If Len(frmName) > 0 Then
                cmbFrms.AddItem frmName
            End If
        Next
    End If
    
    If cmbFrms.ListCount >= 1 Then
        cmbFrms.ListIndex = 0      '´¥·¢cmbFrms_Click
    Else
        CmdGenCode.Enabled = False
        CmdCopyToClipboard.Enabled = False
        CmdSaveToFile.Enabled = False
        mnuSaveToFile.Enabled = False
        mnuCopyToClipboard.Enabled = False
        mnuAddProperty.Enabled = False
        mnuGenCode.Enabled = False
    End If
    
End Sub

Private Sub CmdSaveToFile_Click()
    Me.PopupMenu mnuSaveToFile
End Sub

Private Sub Form_QueryUnload(Cancel As Integer, UnloadMode As Integer)
    Dim I As Long
    If TxtCode.Width = Me.ScaleWidth Then
        TxtCode_DblClick
        Cancel = True
    ElseIf TxtTips.Width = Me.ScaleWidth Then
        TxtTips_DblClick
        Cancel = True
    ElseIf UBound(m_saTmpFile) > 0 Then 'É¾³ýÓÃÓÚÔ¤ÀÀµÄÁÙÊ±ÎÄ¼þ
        On Error Resume Next
        For I = 0 To UBound(m_saTmpFile)
            If Len(m_saTmpFile(I)) Then
                Kill m_saTmpFile(I)
            End If
        Next
        On Error GoTo 0
        ReDim m_saTmpFile(0) As String
    End If
End Sub

Private Sub Form_Resize()
    
    Dim sLstCfgWidth As Single
    
    If Me.WindowState = vbMinimized Then
        Exit Sub
    End If
    
    If m_TxtCodeExpanded Then
        TxtCode_DblClick
    ElseIf m_TxtTipsExpanded Then
        TxtTips_DblClick
    End If
    
    If Me.Width < 9000 Then Me.Width = 9000
    If Me.Height < 6750 Then Me.Height = 6750
    
    If LstCfg.ItemCount > 0 Then LstCfg.TopRow = 0  '¹æ±Ü´Ë¿Ø¼þµÄÒ»¸öBUG£¬Õâ¸öBUG¿ÉÄÜ»áµ¼ÖÂ²¿·ÖÏîÄ¿ÏÔÊ¾²»È«
    
    ResizeForm Me
    
    'µ÷ÕûÅäÖÃÁÐ±í¿òµÄÁÐ¿í¶È
    sLstCfgWidth = GetOrignalWidth(LstCfg)
    If LstCfg.Width < sLstCfgWidth Then
        LstCfg.ColWidth(0) = 2260
        LstCfg.ColWidth(1) = 3450
    ElseIf sLstCfgWidth > 1 Then 'È·ÈÏ¿í¶ÈÖµÓÐÐ§
        LstCfg.ColWidth(0) = 2260 * LstCfg.Width / sLstCfgWidth
        LstCfg.ColWidth(1) = 3450 * LstCfg.Width / sLstCfgWidth
    End If
    
End Sub

Private Sub LstCfg_ItemChecked(Row As Long)
    If InStr(1, " x, y, relx, rely, width, height, relwidth, relheight,", " " & LstCfg.CellText(Row, 0) & ",") Then
        LstCfg.ItemChecked(Row) = True
    ElseIf LstComps.ListCount > 0 And LstComps.ListIndex >= 0 Then '¶îÍâ±ØÑ¡µÄÊôÐÔ
        Select Case Mid$(LstComps.List(LstComps.ListIndex), InStrRev(LstComps.List(LstComps.ListIndex), " ") + 1)
            Case "(TextBox)"
                If LstCfg.CellText(Row, 0) = "textvariable" Then LstCfg.ItemChecked(Row) = True
            Case "(Line)"
                If LstCfg.CellText(Row, 0) = "orient" Then LstCfg.ItemChecked(Row) = True
        End Select
    End If
    
    '¸üÐÂÁÐ±íÖÐµÄÊýÖµµ½ÊµÀý¶ÔÏóºÍÊý×é
    UpdateCfgtoCls m_PrevCompIdx
    
End Sub

Private Sub LstCfg_KeyDown(KeyCode As Integer, Shift As Integer)
    If (KeyCode = &H41 Or KeyCode = &H61) And Shift = vbCtrlMask Then 'Ctrl+a:È«Ñ¡£¬Ö÷ÒªÓÃÓÚ²âÊÔÄ¿µÄ
        Dim I As Long
        LstCfg.Redraw = False
        For I = 0 To LstCfg.ItemCount - 1
            LstCfg.ItemChecked(I) = True
        Next
        LstCfg.Redraw = True
    End If
End Sub

Private Sub LstCfg_RequestEdit(ByVal Row As Long, ByVal Col As Long, Cancel As Boolean)
    
    If Col = 0 Or InStr(1, " x, y, relx, rely, width, height, relwidth, relheight, ", " " & LstCfg.CellText(Row, 0) & ",") Then
        Cancel = True
    Else
        '×Ô¶¯Ìí¼ÓËùÐèµÄÊôÐÔÑ¡ÔñÏî£¬´æÔÚ¿ÉÑ¡ÁÐ±íÊ±£¬ÔÚÏÂÀ­ÁÐ±íÖÐÑ¡Ôñ£¬·ñÔòÇÐ»»³ÉÎÄ±¾¿ò
        FillcmbEdit Row, Col
    End If
    
End Sub

Private Sub LstCfg_RequestUpdate(ByVal Row As Long, ByVal Col As Long, NewValue As String, Cancel As Boolean)
    If NewValue <> "" Then
        LstCfg.ItemChecked(Row) = True
    End If
End Sub

Private Sub LstCfg_RowColChanged()
    If LstComps.ListIndex >= 0 Then
        TxtTips.Text = g_Comps(LstComps.ListIndex).Tips(LstCfg.CellText(LstCfg.Row, 0))
    End If
End Sub

Private Sub LstComps_Click()
    
    Dim ctl As Object, s As String
    
    If LstComps.ListCount = 0 Or LstComps.ListIndex < 0 Then Exit Sub
    
    On Error Resume Next
    s = m_curFrm.Name
    If Err.Number Then
        If MsgBox(L("l_msgGetAttrOfFrmFailed", "Failed in getting property of the form, please reopen the vb project and retry.\nRefresh list of forms now?"), vbInformation + vbYesNo) = vbYes Then
            CmdRefsFormsList_Click
        End If
        Exit Sub
    End If
    Err.Clear
    On Error GoTo 0
    
    '¸üÐÂÁÐ±íÖÐµÄÊýÖµµ½ÊµÀý¶ÔÏó
    UpdateCfgtoCls m_PrevCompIdx
    
    FetchCfgFromCls LstComps.ListIndex
    
    m_PrevCompIdx = LstComps.ListIndex
    
    'ÏÔÊ¾¿Ø¼þÃèÊö
    TxtTips.Text = g_Comps(LstComps.ListIndex).Description
    
    'Ñ¡Ôñ¶ÔÓ¦µÄ¿Ø¼þ
    m_curFrm.Designer.SelectedVBControls.Clear
    For Each ctl In m_curFrm.Designer.VBControls
        If ctl.Properties("Name") = Left(LstComps.List(LstComps.ListIndex), InStr(1, LstComps.List(LstComps.ListIndex), " ") - 1) Then
            ctl.InSelection = True
            Exit For
        End If
    Next
    
End Sub

'ÔÚ¶ÔÏóÖÐ»ñÈ¡ÅäÖÃÐÅÏ¢µ½ÁÐ±í¿ò
Private Sub FetchCfgFromCls(idx As Long)
    
    Dim nRow As Long, cfg As Variant, cItms As Collection
    
    If idx < 0 Or idx > UBound(g_Comps) Then Exit Sub
    
    LstCfg.Redraw = False
    If LstCfg.ItemCount > 0 Then LstCfg.TopRow = 0  'Õâ¸ö²Ù×÷ÊÇÎªÁË¹æ±ÜGridOcxµÄ¹ö¶¯ÌõBUG£¬¾ÍÊÇÇÐ»»¿Ø¼þºóÓÐ²¿·ÖÏîÄ¿ÎÞ·¨ÍêÕûÏÔÊ¾
    LstCfg.Clear
    Set cItms = g_Comps(idx).Allitems()
    For Each cfg In cItms
        nRow = LstCfg.AddItem(Left(cfg, InStr(1, cfg, "|") - 1))
        LstCfg.CellText(nRow, 1) = Mid(cfg, InStr(1, cfg, "|") + 1, InStrRev(cfg, "|") - InStr(1, cfg, "|") - 1)
        LstCfg.ItemChecked(nRow) = CLng(Mid(cfg, InStrRev(cfg, "|") + 1))
    Next
    LstCfg.Redraw = True
    
End Sub

'¸üÐÂÅäÖÃµ½ÊµÀý¶ÔÏó,idx±íÊ¾µ±Ç°ÔÚLstCfgÉÏÏÔÊ¾µÄÊôÐÔÊÇÊôÓÚÄÄ¸ö¿Ø¼þµÄ¡£
Private Sub UpdateCfgtoCls(idx As Long)
    Dim s As String, I As Long
    
    If idx < 0 Or idx > UBound(g_Comps) Then Exit Sub
    
    LstCfg.UpdateIfPending
    LstCfg.Refresh
    
    s = ""
    For I = 0 To LstCfg.ItemCount - 1
        If LstCfg.ItemChecked(I) Then
            s = s & IIf(I > 0, "|", "") & LstCfg.CellText(I, 0) & "|" & LstCfg.CellText(I, 1)
        End If
    Next
    
    If Len(s) Then g_Comps(idx).SetConfig s
    
End Sub

Private Sub mnuAbout_Click()
    frmAbout.Show vbModal
End Sub

'Ôö¼Ó×Ô¶¨ÒåÅäÖÃ
Private Sub mnuAddProperty_Click()
    
    Dim s As String, sa() As String, nRow As Long, I As Long
    
    If LstCfg.ItemCount <= 0 Then Exit Sub
    
    s = InputBox(L("l_ProForAddAttr", "Please input string format as 'Property=Value', for example x=20.\nCase sensitive."), App.Title)
    s = Trim(s)
    If Len(s) <= 0 Then
        Exit Sub
    End If
    
    sa = Split(s, "=")
    If UBound(sa) < 1 Then Exit Sub
    
    ' Èç¹ûÊäÈëµÄÊôÐÔÒÑ¾­´æÔÚ£¬Ôò¸²¸ÇÔ­ÓÐµÄÖµ
    sa(0) = Trim(sa(0))
    For I = 0 To LstCfg.ItemCount - 1
        If LstCfg.CellText(I, 0) = sa(0) Then
            LstCfg.CellText(I, 1) = Trim(sa(1))
            Exit For
        End If
    Next
    'ÐÂÔöÒ»¸öÊôÐÔ
    If I >= LstCfg.ItemCount Then
        I = LstCfg.AddItem(Trim(sa(0)))
        LstCfg.CellText(I, 1) = Trim(sa(1))
    End If
    
    LstCfg.ItemChecked(I) = True
    UpdateCfgtoCls m_PrevCompIdx
    
End Sub

Private Sub mnuCopyToClipAll_Click()
    Clipboard.Clear
    Clipboard.SetText TxtCode.Text
End Sub

Private Sub mnuCopyToClipUiOnly_Click()
    
    Dim s As String, nui As Long, napp As Long, idx As Long
    
    '·ÖÎö´úÂë£¬½öÌáÈ¡Application_ui(),Ê¹ÓÃÕýÔò±í´ïÊ½Ò²¿ÉÒÔ£¬µ«ÊÇÕâÀïÊ¹ÓÃ¼òµ¥×Ö·û´®·ÖÎö
    s = TxtCode.Text
    nui = InStr(1, s, "class Application_ui(Frame):")
    napp = InStr(1, s, "class Application(Application_ui):")
    If nui > 0 And napp > 0 Then
        Clipboard.Clear
        Clipboard.SetText Mid(s, nui, napp - nui - 2) 'É¾³ýÒ»¸ö»Ø³µ»»ÐÐ
    Else
        MsgBox L("l_msgNoClsUi", "Class 'Application_ui' no founded in code!"), vbInformation
    End If
    
End Sub

'Ê¹ÓÃbase64±àÂëÎÄ¼þ
Private Sub mnuEncodeAFile_Click()
    frmEncodeAFile.Show vbModal
End Sub

'¼ì²é¸üÐÂ
Private Sub mnuCheckUpdate_Click()
    Dim data As String, ver As String, currVer As String, skipVer As String
    Dim info As Variant, lastestVerInfo As Variant
    data = HttpGetResponse(OFFICIAL_UPDATE_INFO)
    If Len(data) = 0 Then
        MsgBox L("l_msgFetchVerInfoFail", "Error fetching version update information."), vbInformation
        Exit Sub
    End If
    data = Trim(data)
    ParseJSONString2 data, info
    If IsArray(info) = False Then
        MsgBox L("l_msgParseVerInfoFail", "Unable to parse the version information."), vbInformation
        Exit Sub
    End If
    If IsObject(info(0)) = False Then
        MsgBox L("l_msgParseVerInfoFail", "Unable to parse the version information."), vbInformation
        Exit Sub
    End If
    
    On Error GoTo verErrHandler:
    Set lastestVerInfo = info(0)  'µÚÒ»¸öÔªËØÎª×îÐÂ°æ±¾£¬ÎªÒ»¸ö×Öµä¶ÔÏó
    ver = lastestVerInfo("tag_name") '×÷ÕßÔÚgithubÉÏ·¢²¼°æ±¾Ê±Ê¹ÓÃtag_name±£´æ°æ±¾ºÅ
    If Len(ver) > 0 Then
        currVer = g_AppVerString
        If isVersionNewerThan(ver, currVer) Then
            Load frmNewVer
            frmNewVer.lblInfo.Caption = L("l_msgFoundNewVersion", "Found new version: ") & ver
            frmNewVer.lblInfo.Tag = ver
            frmNewVer.Show vbModal
            Exit Sub
        End If
    End If
verErrHandler:
    MsgBox L("l_msgYourVerIsLastest", "Your version is lastest."), vbInformation
End Sub

Private Sub mnuFile_Click()
    mnuGenCode.Enabled = LstComps.ListCount > 0
End Sub

Private Sub mnuGenCode_Click()
    CmdGenCode_Click
End Sub

Private Sub mnuI18n_Click()
    Dim o As Object
    mnuI18n.Checked = Not mnuI18n.Checked
    SaveSetting App.Title, "Settings", "i18n", IIf(mnuI18n.Checked, "1", "0")
End Sub

Private Sub mnuLng_Click(Index As Integer)
    Dim I As Long
    
    If m_nLngNum = 0 Then Exit Sub
    
    For I = 0 To m_nLngNum - 1
        mnuLng(I).Checked = False
    Next
    
    mnuLng(Index).Checked = True
    SaveSetting App.Title, "Settings", "Language", mnuLng(Index).Caption
    
    ChangeLanguage (mnuLng(Index).Caption)
End Sub

Private Sub mnuSaveAll_Click()
    Dim sF As String
    sF = FileDialog(Me, True, L("l_fdSave", "Save file to:"), "*.py", m_prevsf)
    
    If Len(sF) Then
        If Len(FileExt(sF)) = 0 Then sF = sF & ".py"  'Èç¹ûÎÄ¼þÃûÃ»ÓÐÀ©Õ¹Ãû£¬×Ô¶¯Ìí¼Ó.pyÀ©Õ¹Ãû
        Utf8File_Write_VB sF, TxtCode.Text
    End If
    
    m_prevsf = sF
End Sub

'½öÊä³ö½çÃæÉú³ÉÀà£¬ÓÃÓÚÖ®Ç°ÒÑ¾­½¨ºÃ¿ò¼Ü£¬²¢ÇÒÒ²Ð´ÁËÒ»Ð©´úÂë£¬ÏÖÔÚÐÞ¸Ä¿Õ¼ä²¼¾Ö£¬²»ÓÃÓ°ÏìÆäËû´úÂë
Private Sub mnuSaveUiOnly_Click()
    
    Dim sF As String, s As String, nui As Long, napp As Long
    
    '·ÖÎö´úÂë£¬½öÌáÈ¡main(),Ê¹ÓÃÕýÔò±í´ïÊ½Ò²¿ÉÒÔ£¬µ«ÊÇÕâÀïÊ¹ÓÃ¼òµ¥×Ö·û´®·ÖÎö
    s = TxtCode.Text
    nui = InStr(1, s, "class Application_ui(Frame):")
    napp = InStr(1, s, "class Application(Application_ui):")
    If nui > 0 And napp > 0 Then
        sF = FileDialog(Me, True, L("l_fdSave", "Save file to:"), "*.py", m_prevsf)
        If Len(sF) Then
            If Len(FileExt(sF)) = 0 Then sF = sF & ".py"  'Èç¹ûÎÄ¼þÃûÃ»ÓÐÀ©Õ¹Ãû£¬×Ô¶¯Ìí¼Ó.pyÀ©Õ¹Ãû
            Utf8File_Write_VB sF, Mid(s, nui, napp - nui)
        End If
    Else
        MsgBox L("l_msgNoClsUi", "Class 'Application_ui' no founded in code!"), vbInformation
    End If
    
    m_prevsf = sF
    
End Sub

Private Sub mnuPreview_Click()
    
    Dim bExeExisted As Boolean, sTmpFile As String
    Dim nIdxIcon1 As Long, nIdxIcon2 As Long, s As String, sCode As String, sFrmFile As String
    
    'Ê×ÏÈÅÐ¶ÏPYTHONEXEÊÇ·ñ´æÔÚ
    If Len(g_PythonExe) = 0 Then
        mnuPythonExe_Click
    ElseIf Dir(g_PythonExe) = "" Then
        g_PythonExe = ""
        mnuPythonExe_Click  'Èç¹ûÃ»ÓÐÉèÖÃpython.exeÎ»ÖÃ£¬ÔòÏÈ´ò¿ªÉèÖÃ½çÃæ½øÐÐÉèÖÃ
    Else
        bExeExisted = True
    End If
    
    'Ç°ÃæÉèÖÃºóÔÙÈ·ÈÏÊÇ·ñÕýÈ·£¬²»ÕýÈ·ÔòÍË³ö
    If Not bExeExisted And Len(g_PythonExe) = 0 Then
        Exit Sub
    End If
    
    '´´½¨Ò»¸öÁÙÊ±ÎÄ¼þ
    sTmpFile = CreateTempFile("vt")
    If Len(sTmpFile) Then
        'ÔÝ´æÁÙÊ±ÎÄ¼þÃû£¬ÔÚADDINÍË³öÊ±È«²¿É¾³ý
        ReDim Preserve m_saTmpFile(UBound(m_saTmpFile) + 1) As String
        m_saTmpFile(UBound(m_saTmpFile)) = sTmpFile
        
        sCode = TxtCode.Text
        
        'ÎªÁËÌåÑé¸üºÃ£¬Èç¹ûÉèÖÃÁËÍ¼±êµÄ»°£¬Ôò½«Í¼±êÒ²¿½±´µ½ÁÙÊ±ÎÄ¼þ¼Ð[¼Ù¶¨Í¼±êÔÚ´°ÌåÎÄ¼þÍ¬Ò»¸öÄ¿Â¼]
        nIdxIcon1 = InStr(1, sCode, ".iconbitmap(default=r'")
        If nIdxIcon1 > 0 Then
            nIdxIcon1 = nIdxIcon1 + Len(".iconbitmap(default=r'")
            nIdxIcon2 = InStr(nIdxIcon1 + 1, sCode, "'")
            If nIdxIcon2 > 0 And (nIdxIcon2 - nIdxIcon1 < 256) Then
                s = Mid$(sCode, nIdxIcon1, nIdxIcon2 - nIdxIcon1)
                On Error Resume Next
                sFrmFile = m_curFrm.FileNames(1)
                On Error GoTo 0
                If Len(sFrmFile) Then
                    On Error Resume Next
                    FileCopy AddSlash(PathName(sFrmFile)) & s, AddSlash(PathName(sTmpFile)) & s
                    If Err.Number = 0 Then
                        ReDim Preserve m_saTmpFile(UBound(m_saTmpFile) + 1) As String
                        m_saTmpFile(UBound(m_saTmpFile)) = AddSlash(PathName(sTmpFile)) & s
                        sCode = Left$(sCode, nIdxIcon1 - 1) & m_saTmpFile(UBound(m_saTmpFile)) & Mid$(sCode, nIdxIcon2)
                    End If
                    On Error GoTo 0
                End If
            End If
        End If
        
        Utf8File_Write_VB sTmpFile, sCode
        Shell Chr(34) & g_PythonExe & """ """ & sTmpFile & Chr(34)
    Else
        MsgBox L("l_msgCreateTempFileFailed", "Failed in creating a temp file."), vbInformation
    End If
    
End Sub

Private Sub mnuPythonExe_Click()
    Dim sExe As String, sExes() As String, I As Long
    
    Load frmOption
    
    '½«ÏµÍ³ÖÐËùÓÐÒÑ¾­°²×°µÄPython¶¼Ìí¼Óµ½×éºÏ¿òÖÐÌá¹©Ñ¡Ôñ
    frmOption.cmbPythonExe.Clear
    sExes = GetAllInstalledPython()
    If UBound(sExes) >= 0 Then
        For I = 0 To UBound(sExes)
            frmOption.cmbPythonExe.AddItem sExes(I)
        Next
    End If
    
    '´Ó×¢²á±íÖÐ¶ÁÈ¡±£´æµÄÅäÖÃ
    sExe = GetSetting(App.Title, "Settings", "PythonExe", "")
    If Len(sExe) > 0 Then
        If Dir(sExe) = "" Then
            sExe = ""
        End If
    End If
    
    If Len(sExe) Then
        frmOption.cmbPythonExe.Text = sExe
    ElseIf frmOption.cmbPythonExe.ListCount > 0 Then
        frmOption.cmbPythonExe.ListIndex = 0
    End If
    
    frmOption.Show vbModal  'ÔÚfrmOption´°ÌåÖÐ»áÉèÖÃg_PythonExe±äÁ¿²¢±£´æµ½×¢²á±í
    
End Sub

Private Sub mnuQuit_Click()
    mConnect.Hide
End Sub

Private Sub mnuRefreshForms_Click()
    CmdRefsFormsList_Click
End Sub

Private Sub mnuRelPos_Click()
    Dim o As Object
    mnuRelPos.Checked = Not mnuRelPos.Checked
    
    '¾ø¶Ô×ø±ê
'    If Not m_curFrm Is Nothing Then
'        If Not mnuRelPos.Checked And m_curFrm.Properties("ScaleMode") <> vbTwips Then
'            'Èç¹ûÊ¹ÓÃ¾ø¶Ô×ø±ê£¬²»Ö§³ÖFrame¿Ø¼þ
'            For Each o In m_curFrm.Designer.VBControls
'                If o.ClassName = "Frame" Then
'                    MsgBox L("l_msgFrameNotSupportInAbs", "The control 'Frame' is not support when menu 'Use Relative Position' unchecked."), vbInformation
'                    mnuRelPos.Checked = True
'                    Exit For
'                End If
'            Next
'        End If
'    End If
'
    SaveSetting App.Title, "Settings", "RelPos", IIf(mnuRelPos.Checked, "1", "0")
End Sub

Private Sub mnuUnicodePrefixU_Click()
    
    If MsgBox(L("l_msgChangePrefixU", "Change the prefix of UNICODE string will refresh all widgets of the form, \nContinue?"), vbQuestion + vbOKCancel) = vbCancel Then
        Exit Sub
    End If
    
    mnuUnicodePrefixU.Checked = Not mnuUnicodePrefixU.Checked
    g_bUnicodePrefixU = mnuUnicodePrefixU.Checked
    SaveSetting App.Title, "Settings", "UnicodePrefix", IIf(mnuUnicodePrefixU.Checked, "1", "0")
    
    CmdRefsFormsList_Click
    
End Sub

Private Sub mnuUseTtk_Click()
    Dim I As Long, s As String
    
    If LstComps.ListCount > 0 And LstComps.ListIndex >= 0 Then
        If InStr(1, LstComps.List(LstComps.ListIndex), "ComboBox") Then
            LstComps_Click                                                      'ÏÈ±£´æÅäÖÃ£¬±ÜÃâÍòÒ»×éºÏ¿òÇÐ»»ºóÅäÖÃ²»¶Ô
        End If
    End If
    
    mnuUseTtk.Checked = Not mnuUseTtk.Checked
    
    'ÅÐ¶ÏÊÇ·ñÓÐTTK¶¼ÓÐµÄ¿Ø¼þ£¬Èç¹ûÓÐ£¬Ôò²»ÔÊÐíÈ¡ÏûTTKÑ¡Ïî
    If Not mnuUseTtk.Checked Then
        For I = 0 To LstComps.ListCount - 1
            s = Mid(LstComps.List(I), InStr(1, LstComps.List(I), "(") + 1)
            s = Left(s, Len(s) - 1)
            If InStr(1, " ProgressBar, TreeView, TabStrip, Line, ", " " & s & ",") > 0 Then
                MsgBox L("l_msgCantCancelTTK", "Can't uncheck the menu 'Use TTK Themed Library' for has some widgets specified in TTK."), vbInformation
                mnuUseTtk.Checked = True
                Exit For
            End If
        Next
    End If
    
    'ÇÐ»»×éºÏ¿òÊÊÅäÆ÷µÄTTKÊôÐÔ
    If LstComps.ListCount > 0 Then
        For I = 0 To UBound(g_Comps)
            If TypeName(g_Comps(I)) = "clsComboboxAdapter" Then
                g_Comps(I).TTK = mnuUseTtk.Checked
            End If
        Next
        
        If LstComps.ListIndex >= 0 Then
            If InStr(1, LstComps.List(LstComps.ListIndex), "ComboBox") Then
                FetchCfgFromCls LstComps.ListIndex                              'ÖØÐÂ»ñÈ¡×éºÏ¿òÐÅÏ¢
            End If
        End If
        LstComps_Click
    End If
    
    SaveSetting App.Title, "Settings", "UseTtk", IIf(mnuUseTtk.Checked, "1", "0")
    
End Sub

Private Sub mnuV2andV3Code_Click()
    mnuV2andV3Code.Checked = Not mnuV2andV3Code.Checked
    SaveSetting App.Title, "Settings", "V2andV3Code", IIf(mnuV2andV3Code.Checked, "1", "0")
End Sub

'×Ô¶¯Ìî³ä±à¼­ÓÃµÄ×éºÏ¿òÄÚÈÝ
Private Sub FillcmbEdit(Row As Long, Col As Long)
    
    Dim sa() As String, I As Long, nEditType As Long, fn As String
    Static s_NoFirstcmbEditList As Boolean, s_NoFirstcmbEditCombo As Boolean
    
    If LstComps.ListCount = 0 Or LstComps.ListIndex < 0 Then Exit Sub
    
    '0±íÊ¾ÄÚÖÃÎÄ±¾±à¼­¿ò£¬1±íÊ¾½öÏÞÏÂÀ­ÁÐ±í£¬2±íÊ¾ÏÂÀ­ÁÐ±í¼ÓÎÄ±¾ÊäÈë
    nEditType = g_Comps(LstComps.ListIndex).GetAttrValueList(LstCfg.CellText(Row, 0), sa)
    
    If nEditType = 1 Then
        LstCfg.BindControl 1, cmbEditList
        cmbEditList.Clear
        cmbEditList.AddItem "" 'ÔÚµÚÒ»ÐÐ·ÅÒ»¸ö¿Õ×Ö·û´®£¬ÕâÑù¾Í¿ÉÒÔ²»ÉèÖÃ¶ÔÓ¦µÄ²ÎÊý¡£
        For I = 0 To UBound(sa)
            cmbEditList.AddItem sa(I)
        Next
        For I = 0 To cmbEditList.ListCount - 1
            If cmbEditList.List(I) = LstCfg.CellText(Row, Col) Then
                cmbEditList.ListIndex = I
                Exit For
            End If
        Next
        cmbEditList.Refresh
    ElseIf nEditType = 2 Then
        LstCfg.BindControl 1, cmbEditCombo
        cmbEditCombo.Clear
        For I = 0 To UBound(sa)
            cmbEditCombo.AddItem sa(I)
        Next
        cmbEditCombo.Text = LstCfg.CellText(Row, Col)
        cmbEditCombo.Refresh
    Else
        LstCfg.BindControl 1, Nothing  'Ê¹ÓÃÄÚÖÃÎÄ±¾±à¼­¿ò
    End If
    
End Sub

Private Sub stabar_DblClick()
    MsgBox L("l_msgCtlsSupported", "Controls supported:") & vbCrLf & "Menu, Label, TextBox, PictureBox, Frame, CommandButton, CheckBox, OptionButton, ComboBox," & vbCrLf & _
            "ListBox, HScrollBar, VScrollBar, Slider, ProgressBar, TreeView, StatusBar, CommonDialog, Line" & vbCrLf & vbCrLf
End Sub

Private Sub stabar_MouseUp(Button As Integer, Shift As Integer, x As Single, y As Single)
    If Shift = vbCtrlMask Then
        Clipboard.Clear
        Clipboard.SetText OFFICIAL_SITE
    End If
End Sub

Private Sub TxtCode_Change()
    mnuPreview.Enabled = (Len(TxtCode.Text) > 0)
End Sub

Private Sub TxtCode_DblClick()
    Static s_l As Single, s_t As Single, s_w As Single, s_h As Single
    Static s_txt As String
    
    If m_TxtCodeExpanded Then
        TxtCode.Move s_l, s_t, s_w, s_h
        m_TxtCodeExpanded = False
    Else
        s_l = TxtCode.Left
        s_t = TxtCode.Top
        s_w = TxtCode.Width
        s_h = TxtCode.Height
        TxtCode.ZOrder 0
        TxtCode.Move 0, 0, Me.ScaleWidth, Me.ScaleHeight
        m_TxtCodeExpanded = True
    End If
End Sub

Private Sub TxtCode_KeyUp(KeyCode As Integer, Shift As Integer)
    If KeyCode = vbKeyEscape And TxtCode.Width = Me.ScaleWidth Then
        TxtCode_DblClick
    End If
End Sub

'²é¿´Êó±êºÍ°´¼üÏûÏ¢ÏêÇé
Private Sub TxtTips_DblClick()
    Static s_l As Single, s_t As Single, s_w As Single, s_h As Single
    Static s_txt As String
    
    Dim s As String
    s = TxtTips.Text
    If Len(s) Then
        If Left(s, Len("bindcommand")) = "bindcommand" Then
            s_l = TxtTips.Left
            s_t = TxtTips.Top
            s_w = TxtTips.Width
            s_h = TxtTips.Height
            TxtTips.ZOrder 0
            TxtTips.Move 0, 0, Me.ScaleWidth, Me.ScaleHeight
            s_txt = TxtTips.Text
            m_TxtTipsExpanded = True
            TxtTips.Text = "<ÔÙ´ÎË«»÷·µ»Ø>" & vbCrLf & _
            "bindcommand" & vbCrLf & _
            "Ê¹ÓÃbind()°ó¶¨µÄÊÂ¼þ´¦ÀíÊÂ¼þÁÐ±í£¬ÐèÒª°ó¶¨¶à¸öÔòÊ¹ÓÃ¶ººÅ·Ö¸ô£¬Èç¹û²»ÐèÒªÔòÁô¿Õ¡£" & vbCrLf & _
            "ËùÓÐÊÂ¼þÁÐ±íÈçÏÂ£º" & vbCrLf & _
            "<ButtonPress-n> : Êó±ê°´Å¥n°´ÏÂ£¬n:1(×ó¼ü);2(ÖÐ¼ü);3(ÓÒ¼ü)" & vbCrLf & _
            "<Button-n>,<n> : ¶¼ÊÇ<ButtonPress-n>µÄ¼ò»¯ÐÎÊ½" & vbCrLf & _
            "<ButtonRelease-n> : Êó±ê°´Å¥n±»ËÉ¿ª" & vbCrLf & _
            "<Bn-Motion> : ÔÚ°´×¡°´Å¥nµÄÍ¬Ê±£¬Êó±ê·¢ÉúÒÆ¶¯" & vbCrLf & _
            "<Double-Button-n> : Êó±ê°´Å¥nË«»÷" & vbCrLf & _
            "<Triple-Button-n> : Êó±ê°´Å¥nÈý»÷" & vbCrLf & _
            "<Enter> : Êó±êÖ¸Õë½øÈë×é¼þ" & vbCrLf & _
            "<Leave> : Êó±êÖ¸ÕëÀë¿ª×é¼þ" & vbCrLf & _
            "<FocusIn> / <FocusOut> : ×é¼þ»ñµÃ»òÊ§È¥½¹µã" & vbCrLf & _
            "<KeyPress> : °´ÏÂÈÎÒâ¼ü" & vbCrLf & _
            "<KeyRelease> : ËÉ¿ªÈÎÒâ¼ü" & vbCrLf & _
            "<KeyPress-key> : °´ÏÂkey£¬±ÈÈç<KeyPress-H>±íÊ¾°´ÏÂH¼ü£¬¿ÉÒÔ¼ò»¯ÎªÊ¹ÓÃË«ÒýºÅ´úÌæ¼âÀ¨ºÅ½«×Ö·ûÀ¨ÆðÀ´£¬±ÈÈç£º""H""¡£" & vbCrLf & _
            "<KeyRelease-key> : ËÉ¿ªkey" & vbCrLf & _
            "<Key> : °´ÏÂÈÎÒâ¼ü¡£" & vbCrLf & _
            "<Key-key> : <KeyPress-key>µÄ¼ò»¯ÐÎÊ½£¬±ÈÈç<Key-H>¡£" & vbCrLf & _
            "<key> : Ê¹ÓÃºó¸½µÄÌØÊâ¼ü¶¨ÒåÌæ»»key£¬±íÊ¾°´ÏÂÌØ¶¨¼ü¡£" & vbCrLf & _
            "<Prefix-key> : ÔÚ°´×¡PrefixµÄÍ¬Ê±£¬°´ÏÂkey£¬¿ÉÒÔÊ¹ÓÃAlt,Shift,ControlµÄµ¥¸ö»ò×éºÏ±ÈÈç<Control-Alt-key>" & vbCrLf
            
            TxtTips.Text = TxtTips.Text & "<Configure> : ¿Ø¼þ´óÐ¡¸Ä±äºó´¥·¢¡£" & vbCrLf & _
            "¸½È«²¿ÌØÊâ¼ü¶¨Òå£º" & vbCrLf & _
            "Cancel,Break,BackSpace,Tab,Return," & vbCrLf & _
            "Sift_L , Shift_R, Control_L, Control_R, Alt_L, Alt_R, Pause" & vbCrLf & _
            "Caps_Loack,Escape,Prior(PageUp),Next(PageDown),End,Home,Left,Up,Right,Down,Print," & vbCrLf & _
            "Insert,Delete,F1-12,Num_Lock,Scroll_Lock,space,less"
            TxtTips.SelStart = 1
            TxtTips.SelLength = 0
        ElseIf Left(s, Len("<ÔÙ´ÎË«»÷·µ»Ø>")) = "<ÔÙ´ÎË«»÷·µ»Ø>" Then
            TxtTips.Move s_l, s_t, s_w, s_h
            TxtTips.Text = s_txt
            m_TxtTipsExpanded = False
        End If
    End If
End Sub

Private Sub TxtTips_KeyUp(KeyCode As Integer, Shift As Integer)
    If KeyCode = vbKeyEscape And TxtTips.Width = Me.ScaleWidth Then
        TxtTips_DblClick
    End If
End Sub

Private Sub TxtTips_MouseMove(Button As Integer, Shift As Integer, x As Single, y As Single)
    stabar.SimpleText = L("l_staTips", "Tips of properties.")
End Sub

Private Sub LstComps_MouseMove(Button As Integer, Shift As Integer, x As Single, y As Single)
    stabar.SimpleText = L("l_staComps", "List of controls.")
End Sub

Private Sub cmbFrms_GotFocus()
    stabar.SimpleText = L("l_staFrms", "List of forms.")
End Sub

Private Sub CmdCopyToClipboard_MouseMove(Button As Integer, Shift As Integer, x As Single, y As Single)
    stabar.SimpleText = L("l_staCopyCode", "Copy code to clipboard.")
End Sub

Private Sub CmdGenCode_MouseMove(Button As Integer, Shift As Integer, x As Single, y As Single)
    stabar.SimpleText = L("l_staCmdGenCode", "Generate python code after confirm properties of controls.")
End Sub

Private Sub CmdQuit_MouseMove(Button As Integer, Shift As Integer, x As Single, y As Single)
    stabar.SimpleText = L("l_staQuit", "Quit!")
End Sub

Private Sub CmdRefsFormsList_MouseMove(Button As Integer, Shift As Integer, x As Single, y As Single)
    stabar.SimpleText = L("l_staRefsFrms", "Resfresh list of forms and controls.")
End Sub

Private Sub CmdSaveToFile_MouseMove(Button As Integer, Shift As Integer, x As Single, y As Single)
    stabar.SimpleText = L("l_staCmdSaveFile", "Save code to file (format utf-8 with BOM).")
End Sub

Private Sub Form_MouseMove(Button As Integer, Shift As Integer, x As Single, y As Single)
    stabar.SimpleText = ""
End Sub

Private Sub LstCfg_MouseMove(Button As Integer, Shift As Integer, x As Single, y As Single)
    stabar.SimpleText = L("l_staLstCfg", "List of properties of control, F2/Return/DblClick to modify a property.")
End Sub

Private Sub TxtCode_MouseMove(Button As Integer, Shift As Integer, x As Single, y As Single)
    stabar.SimpleText = L("l_staTxtCode", "Preview python code here, DblClick to zoom out/in.")
End Sub
 
Private Sub stabar_MouseMove(Button As Integer, Shift As Integer, x As Single, y As Single)
    stabar.SimpleText = OFFICIAL_SITE & "  ['Ctrl+Click' copy url to clipboard]"
End Sub

